CREATE OR REPLACE PROCEDURE EDM_UPDATE_USER_EMAIL_RPC IS
  TYPE SUBSCRIBE_TYPE IS TABLE OF EDM_SUBSCRIBE%ROWTYPE INDEX BY BINARY_INTEGER;
  TYPE SUBSCRIBE_INFO_TYPE IS TABLE OF EDM_SUBSCRIBE_INFO%ROWTYPE INDEX BY BINARY_INTEGER;
  TYPE SUB_TYPE_ARRAY IS VARRAY(2) OF VARCHAR2(100);
  SUBSCRIBE_INFO_ROW       EDM_SUBSCRIBE_INFO%ROWTYPE;
  SUBSCRIBE_TYPE_LIST      SUBSCRIBE_TYPE;
  SUBSCRIBE_INFO_TYPE_LIST SUBSCRIBE_INFO_TYPE;
  J                        INTEGER := 1;
  SUB_TYPE                 SUB_TYPE_ARRAY;
BEGIN
  SUB_TYPE := NEW
              SUB_TYPE_ARRAY('MARKETING_EMAIL', 'GROUP_PROCUREMENT_EMAIL');
  SELECT EDM_USER_PK_SEQ.NEXTVAL,
         EMAIL,
         (SELECT C.PROVINCE_ID FROM COM_CITY C WHERE C.CITY_ID = U.CITY_ID),
         CITY_ID,
         SYSDATE,
         U.USER_NO,
         NULL,
         NULL,
         'BACKEND' BULK COLLECT
    INTO SUBSCRIBE_TYPE_LIST
    FROM LVMAMA_PET.USER_USER U
   WHERE U.CREATED_DATE >= TRUNC(SYSDATE) - 1
     AND U.EMAIL IS NOT NULL
     AND U.IS_VALID = 'Y'
     AND NOT EXISTS (SELECT 1
            FROM EDM_SUBSCRIBE S
           WHERE S.USER_ID = U.USER_NO
              OR S.EMAIL = U.EMAIL);
  FOR I IN 1 .. SUBSCRIBE_TYPE_LIST.COUNT LOOP
    FOR Y IN SUB_TYPE.FIRST .. SUB_TYPE.LAST LOOP
      SUBSCRIBE_INFO_ROW.ID := EDM_SUB_INFO_PK_SEQ.NEXTVAL;
      SUBSCRIBE_INFO_ROW.EDM_USER_ID := SUBSCRIBE_TYPE_LIST(I).ID;
      SUBSCRIBE_INFO_ROW.TYPE := SUB_TYPE(Y);
      SUBSCRIBE_INFO_ROW.IS_VALID := 'Y';
      SUBSCRIBE_INFO_ROW.CREATE_DATE := SYSDATE;
      SUBSCRIBE_INFO_TYPE_LIST(J) := SUBSCRIBE_INFO_ROW;
      J := J + 1;
    END LOOP;
  END LOOP;
  FORALL X IN 1 .. SUBSCRIBE_TYPE_LIST.LAST
    INSERT INTO EDM_SUBSCRIBE VALUES SUBSCRIBE_TYPE_LIST (X);
  FORALL I IN 1 .. SUBSCRIBE_INFO_TYPE_LIST.LAST
    INSERT INTO EDM_SUBSCRIBE_INFO VALUES SUBSCRIBE_INFO_TYPE_LIST (I);
  COMMIT;
END EDM_UPDATE_USER_EMAIL_RPC;
